* WRITE CRD COORDINATE FILES OF PFF NORMAL MODES 
*

DIMENS CHSIZE 3000000 MAXRES 3000000

bomlev -5
wrnlev -1

! ============================================== !
! WRITE CRD COORDINATE FILES OF PFF NORMAL MODES !
! ============================================== !

! READ TOPOLOGY AND PARAMETER FILES
! file to some write information
open write card unit 22 name /dev/null
outu 22
stream ../inputs/charmm_toppar.str
outu 6

! Read system`s psf file
open read card unit 25 name ../inputs/@psffile
read psf card unit 25
close unit 25

! Define the group formed by the atoms considered in PFF NMA calculation
define PRTN sele segid PRO* end
set Nprtn ?nsel
! Remove all atoms that are not considered in PFF NMA calculation
dele atom sele .not. PRTN end

! open the additional inputfile that includes the NM to write
open read card unit 13 name ../inputs/input.txt

prnlev 5

! Read all modes, select the mode of interest
! and generate the displacement along it
system "wc -l < ../inputs/input.txt > /tmp/lines.txt"
open read card unit 14 name /tmp/lines.txt
get modnu unit 14
set nmloop 0

label writemodes
incr nmloop by 1
get nm unit 13

vibran nmod 406
open read file unit 40 name ../inputs/@modfile
read norm file mode @nm unit 40
fill comp mode 1 fact 1
end 

! When vibran fills comp array with the NM
! it divides the vector by sqrt(m), so we
! mass weight the mode
scalar wmain = mass
scalar wmain sqrt

scalar xcomp prod wmain
scalar ycomp prod wmain
scalar zcomp prod wmain

open write card unit 14 name ../inputs/mode_nm@nm.crd
write coor comp card unit 14

if @nmloop .lt. @modnu goto writemodes

close unit 13

stop
